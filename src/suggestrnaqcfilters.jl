"""
    suggestrnaqcfilters(
        sums::Vector{Float64}, 
        detected::Vector{Int32}, 
        subsetproportions::Dict{String,Vector{Float64}}; 
        block = nothing, 
        nummads = 3)

Suggest suitable filtering thresholds for the RNA-derived QC metrics.
This is based on the assumption that most cells are of high quality, such that outliers for the QC metrics (detected with a MAD-based algorithm) are potentially problematic and should be removed.
The function accepts the following arguments, typically generated by `percellrnaqcmetrics`:

- `sums`, a vector of total counts for each cell.
- `detected`, a vector containing the number of detected genes per cell.
- `subsetproportions`, a dictionary containing the proportion of counts in each feature subset.

All vectors should have length equal to the number of cells.
This function then returns a dictionary containing the following keys:

- `thresholds`: a dictionary containing:
  - `sums`: a vector of doubles of length equal to the number of blocks, containing the filtering threshold applied to the sums.
  - `detected`: a vector of doubles of length equal to the number of blocks, containing the filtering threshold applied to the detected number of genes.
  - `subsetproportions`: a dictionary where each key is a feature subset name and each value is a vector of doubles of length equal to the number of blocks, 
     containing the filtering threshold applied to the subset proportion.
- `filter`: a vector of booleans where a truthy value indicates that the corresponding cell failed to pass at least one of the thresholds.
  Such cells are deemed to be of low-quality and should be discarded.
- `block_levels`: a vector of block levels, to assist interpretation of the `thresholds`.
  Only generated if `block` is supplied.

`block` should be a vector of length equal to the number of columns of `x`.
This should specify the block assignment for each cell.
Filtering thresholds are computed within each block, which avoids inflated MADs when there are large block-to-block differences.
If not provided, all cells are assumed to belong to the same block.

The number of MADs used to define the outlier thresholds is defined by `nummads`.
Larger values yield more relaxed filters.

# Examples
```jldoctest

julia> using SparseArrays

julia> x = abs.(sprand(Int32, 50, 10, 0.2))

julia> using scran

julia> mat = scran.initializesparsematrix(x)

julia> sub = Dict{String,Vector{Int}}("mito" => Vector{Int}([1,2,3,4,5]))

julia> metrics = scran.percellrnaqcmetrics(mat; subsets = sub)

julia> scran.suggestrnaqcfilters(metrics["sums"], metrics["detected"], metrics["subsetproportions"])
```
"""
function suggestrnaqcfilters(sums::Vector{Float64}, detected::Vector{Int32}, subsetproportions::Dict{String,Vector{Float64}}; block = nothing, nummads = 3)
    ncells = length(sums)

    if ncells != length(detected) 
        throw(ErrorException("'sums' and 'detected' should be of the same length"))
    end

    subnames = Vector{String}()
    props = Vector{Any}()
    for (k, v) in subsetproportions
        if length(v) != ncells
            throw(ErrorException("each entry of 'subsetproportions' should be of the same length as 'sums'"))
        end
        push!(props, v)
        push!(subnames, k)
    end

    use_block, block_ids, block_levels = transform_factor(block, ncells, "length of 'block' vector should be equal to length of 'sums'")

    survivors = Vector{UInt8}(undef, ncells)
    thresholds = suggest_rna_qc_filters(sums, detected, props, use_block, block_ids, survivors, nummads)

    prop = Dict{String, Vector{Float64}}()
    for i in eachindex(subnames)
        prop[subnames[i]] = thresholds_proportions(thresholds, i - 1)
    end

    output = Dict{String, Any}(
        "thresholds" => Dict{String, Any}(
            "sums" => copy(thresholds_sums(thresholds)),
            "detected" => copy(thresholds_detected(thresholds)),
            "subsetproportions" => prop
        ),
        "filter" => Vector{Bool}(survivors)
    )

    if use_block
        output["block_levels"] = block_levels
    end

    return output
end
